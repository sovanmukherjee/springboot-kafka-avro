import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.0'
	id "com.github.davidmc24.gradle.plugin.avro" version "1.8.0"
}

group = 'org.poc.kafka.avro'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
	gradlePluginPortal()
	maven {
		url "https://packages.confluent.io/maven"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	//lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	//kafka
	implementation 'org.springframework.kafka:spring-kafka:3.0.11'
	//confluent
	implementation 'io.confluent:kafka-avro-serializer:7.4.0'
	implementation 'io.confluent:kafka-schema-registry-client:7.4.0'
	implementation 'io.confluent:kafka-streams-avro-serde:7.4.0'
	//Gson
	implementation 'com.google.code.gson:gson:2.10.1'
	//test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

def generateAvro = tasks.register("generateAvro", GenerateAvroJavaTask) {
	stringType = "String"
	fieldVisibility = "PRIVATE"
	source 'src/main/resources/avro/schemas/'
	outputDir file('build/generated/')
}

sourceSets {
	getByName("main") {
		java.srcDir("$buildDir/generated")
	}
}

tasks.named("compileJava").configure {
	source(generateAvro)
}

tasks.named('test') {
	useJUnitPlatform()
}
